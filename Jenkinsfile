pipeline {
  agent any

  environment {
    COLLECTION_DIR = "/work/collections/collections"
    REPORT_DIR = "/work/reports"
    HTML_REPORT_DIR = "/work/reports/html"
    ALLURE_RESULTS_DIR = "allure-results"
    ENV_FILE = "/work/collections/environments/DEV.postman_environment.json"
    WEBHOOK_URL = credentials('GOOGLE_CHAT_WEBHOOK')
    BASE_URL = "http://maid-cloud.vir999.com"
    YOUR_TOKEN_ENV_VAR = credentials('0f2edbf7-d6f8-4cf7-a248-d38c89cd99fc')
  }

  stages {
    stage('Checkout Code') {
      steps {
        checkout scm
      }
    }

    stage('Checkout Postman Collections') {
      steps {
        script {
          sh 'rm -rf /work/collections/* || true'
        }
        dir('/work/collections') {
          sh '''
            if [ ! -d .git ]; then
              git clone https://github.com/SR-AM-NoahChang/Maid-postman-auto-tests.git .
            fi
            git fetch origin main
            git reset --hard origin/main
            echo "‚úÖ Áï∂Ââç Git commitÔºö$(git rev-parse HEAD)"
            echo "üìù Commit Ë®äÊÅØÔºö$(git log -1 --oneline)"
          '''
        }
      }
    }

    stage('Prepare Folders') {
      steps {
        script {
          def timestamp = sh(script: "date +%Y%m%d_%H%M%S", returnStdout: true).trim()
          sh """
            mkdir -p /work/report_backup
            if [ -d "${REPORT_DIR}" ]; then
              mv ${REPORT_DIR} /work/report_backup/${timestamp}
              chmod -R 755 /work/report_backup/${timestamp}
              echo üì¶ ÂÇô‰ªΩËàäÂ†±ÂëäÂà∞ /work/report_backup/${timestamp}
            fi
            rm -rf ${REPORT_DIR} ${HTML_REPORT_DIR} allure-results
            mkdir -p ${REPORT_DIR} ${HTML_REPORT_DIR} allure-results
          """
        }
      }
    }

    stage('Run First Collection and Get Workflow ID') {
  steps {
    script {
      def collectionName = "01Áî≥Ë´ãÂª≥‰∏ªË≤∑ÂüüÂêç"
      def collectionPath = "${COLLECTION_DIR}/${collectionName}.postman_collection.json"

      if (!fileExists(collectionPath)) {
        error "‚ùå Êâæ‰∏çÂà∞ collectionÔºö${collectionPath}"
      }

      echo "‚ñ∂Ô∏è Âü∑Ë°å Postman Ê∏¨Ë©¶Ôºö${collectionName}"
      sh """
        newman run "${collectionPath}" \
          --environment "${ENV_FILE}" \
          --insecure \
          --reporters cli,json,html,junit,allure \
          --reporter-json-export "${REPORT_DIR}/${collectionName}_report.json" \
          --reporter-html-export "${HTML_REPORT_DIR}/${collectionName}_report.html" \
          --reporter-junit-export "${REPORT_DIR}/${collectionName}_report.xml" \
          --reporter-allure-export "allure-results" || true
      """

      // Êì∑Âèñ workflow_id
      def report = readJSON file: "${REPORT_DIR}/${collectionName}_report.json"
      def variables = report.run?.executions?.last()?.variableScope ?: []

      def workflowId = variables.find { it.key == "PD_WORKFLOW_ID" }?.value

      // ÂÇôÊè¥ÔºöÂæû console log ‰∏≠Â∞ãÊâæ [workflow_id]::12345
      if (!workflowId) {
        def logText = report.run?.executions?.last()?.console?.join("\n") ?: ""
        def matcher = logText =~ /\[workflow_id\]::(\d+)/
        if (matcher.find()) {
          workflowId = matcher.group(1)
          echo "‚ö†Ô∏è Âæû console log ÂÇôÊè¥ÂèñÂæó workflow_id: ${workflowId}"
        }
      }

      if (!workflowId) {
        error "‚ùå ÁÑ°Ê≥ïÂæû ${collectionName} ÂõûÊáâ‰∏≠ÂèñÂæó workflow_id"
      }

      echo "üìå Êì∑ÂèñÂà∞ workflow_idÔºö${workflowId}"
      env.WORKFLOW_ID = workflowId
    }
  }
}


    stage('Poll Workflow Job Status') {
      steps {
        script {
          def pollMaxAttempts = 10
          def pollIntervalSeconds = 30
          int attempt = 1

          while (attempt <= pollMaxAttempts) {
            echo "‚è≥ Á¨¨ ${attempt} Ê¨°Ëº™Ë©¢ÔºåÊôÇÈñìÔºö${new Date()}"

            sh """
              newman run "${COLLECTION_DIR}/check-job-status.postman_collection.json" \
                --environment "${ENV_FILE}" \
                --env-var workflowId=${env.WORKFLOW_ID} \
                --insecure \
                --reporters cli,json \
                --reporter-json-export job_status.json
            """

            def statusJson = readJSON file: 'job_status.json'
            def variables = statusJson.run.executions[-1].result.collectionVariables

            def failedCount = variables.find { it.key == 'poll_failed_job_count' }?.value?.toInteger() ?: 0
            def pendingCount = variables.find { it.key == 'poll_pending_job_count' }?.value?.toInteger() ?: 0

            echo "üîé Êü•Ë©¢ÁµêÊûúÔºö${failedCount} failed, ${pendingCount} pending"

            if (failedCount > 0) {
              error "‚ùå Ëº™Ë©¢Â§±ÊïóÔºöÊúâ ${failedCount} ÂÄã Job ÁÇ∫ failure"
            }

            if (pendingCount == 0) {
              echo "‚úÖ ÊâÄÊúâ job ÁãÄÊÖãÁÇ∫ successÔºåËº™Ë©¢ÂÆåÊàê"
              break
            }

            echo "üò¥ Á≠âÂæÖ ${pollIntervalSeconds} Áßí..."
            sleep pollIntervalSeconds
            attempt++
          }

          if (attempt > pollMaxAttempts) {
            error "‚ùå Ë∂ÖÈÅéÊúÄÂ§ßËº™Ë©¢Ê¨°Êï∏ (${pollMaxAttempts})ÔºåÊµÅÁ®ãÁµêÊùü"
          }
        }
      }
    }

    stage('Run Remaining Postman Collections') {
      steps {
        script {
          def otherCollections = [
            "02Áî≥Ë´ãÂà™Èô§ÂüüÂêç",
            "03Áî≥Ë´ãÊÜëË≠â",
            "04Áî≥Ë´ãÂ±ïÂª∂ÊÜëË≠â",
            "06Áî≥Ë´ã‰∏âÁ¥ö‰∫ÇÊï∏"
          ]

          otherCollections.each { name ->
            def path = "${COLLECTION_DIR}/${name}.postman_collection.json"
            if (fileExists(path)) {
              echo "‚ñ∂Ô∏è Âü∑Ë°å Postman Ê∏¨Ë©¶Ôºö${name}"
              sh """
                newman run "${path}" \
                  --environment "${ENV_FILE}" \
                  --insecure \
                  --reporters cli,json,html,junit,allure \
                  --reporter-json-export "${REPORT_DIR}/${name}_report.json" \
                  --reporter-html-export "${HTML_REPORT_DIR}/${name}_report.html" \
                  --reporter-junit-export "${REPORT_DIR}/${name}_report.xml" \
                  --reporter-allure-export "allure-results" || true
              """
            } else {
              echo "‚ö†Ô∏è Ë∑≥ÈÅéÔºöÊâæ‰∏çÂà∞ collection Ê™îÊ°àÔºö${path}"
            }
          }
        }
      }
    }

    stage('Merge JSON Results') {
      steps {
        sh "jq -s . ${REPORT_DIR}/*_report.json > ${REPORT_DIR}/suites.json || true"
      }
    }

    stage('Publish HTML Reports') {
      steps {
        publishHTML(target: [
          reportDir: "${HTML_REPORT_DIR}",
          reportFiles: 'index.html',
          reportName: 'Postman HTML Reports',
          allowMissing: true,
          alwaysLinkToLastBuild: true,
          keepAll: true
        ])
      }
    }

    stage('Allure Report') {
      steps {
        allure([
          includeProperties: false,
          jdk: '',
          results: [[path: 'allure-results']]
        ])
      }
    }
  }

  post {
    always {
      script {
        def buildResult = currentBuild.currentResult
        def statusEmoji = buildResult == 'SUCCESS' ? '‚úÖ' : (buildResult == 'FAILURE' ? '‚ùå' : '‚ö†Ô∏è')
        def timestamp = new Date().format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone('Asia/Taipei'))

        def message = """
        {
          \"cards\": [
            {
              \"header\": {
                \"title\": \"${statusEmoji} Jenkins Pipeline Âü∑Ë°åÁµêÊûú\",
                \"subtitle\": \"Â∞àÊ°àÔºö${env.JOB_NAME} (#${env.BUILD_NUMBER})\",
                \"imageUrl\": \"https://uxwing.com/wp-content/themes/uxwing/download/brands-and-social-media/postman-icon.png\",
                \"imageStyle\": \"AVATAR\"
              },
              \"sections\": [
                {
                  \"widgets\": [
                    {
                      \"keyValue\": {
                        \"topLabel\": \"ÁãÄÊÖã\",
                        \"content\": \"${buildResult}\"
                      }
                    },
                    {
                      \"keyValue\": {
                        \"topLabel\": \"ÂÆåÊàêÊôÇÈñì\",
                        \"content\": \"${timestamp}\"
                      }
                    }
                  ]
                }
              ]
            }
          ]
        }
        """
        writeFile file: 'payload.json', text: message
        withEnv(["WEBHOOK=${WEBHOOK_URL}"]) {
          sh 'curl -k -X POST -H "Content-Type: application/json" -d @payload.json "$WEBHOOK"'
        }
      }
    }
  }
}
